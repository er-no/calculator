plugins {
    id 'application'
    id 'jacoco'
    id 'com.github.spotbugs' version '5.0.0-beta.6'
}

repositories {
    mavenCentral() // maven repos
}

dependencies {
    testImplementation 'junit:junit:4.13.2'
    implementation 'com.google.guava:guava:30.1.1-jre' // used by 'application' plugin
}

/* project attributes */

applicationName = 'calculator'
defaultTasks 'test'
compileJava.options.encoding = compileTestJava.options.encoding = 'UTF-8'

/* plugin options */

application {
    mainClass = 'App' // where to find main method
}

jacocoTestReport {
    dependsOn test
}

/* gradle task options */

test {
    testLogging.events 'passed', 'failed' // show all executed tests
    testLogging.exceptionFormat = 'full'  // full eror output on failure
    dependsOn 'cleanTest'                 // causes tests to always be re-run.
    finalizedBy jacocoTestReport          // call for test-coverage report
}

jar {
    manifest.attributes 'Main-Class': application.mainClass
    destinationDirectory = projectDir
    archiveFileName = applicationName + '.jar'
}

clean.dependsOn 'cleanJAR'

/* custom tasks */
task cleanJAR(type: Delete) {
    description 'Remove generated jar file.'
    delete file(applicationName + '.jar')
}
